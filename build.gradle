plugins {
    id 'java'
    id 'jacoco'
}

group = 'me.sungbin'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.10"
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                fileTree(dir: layout.buildDirectory.dir("classes/java/main").get(), exclude: [
                        'me/sungbin/day2/**',
                        'me/sungbin/day4/**',
                        'me/sungbin/day7/**'
                ])
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS' // 클래스 단위로 검증

            limit {
                counter = 'LINE' // 라인 단위로 커버리지 측정
                value = 'COVEREDRATIO' // 커버된 라인 비율 사용
                minimum = 1.00 // 최소 75% 이상 커버리지 필요
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(
                fileTree(dir: layout.buildDirectory.dir("classes/java/main").get(), exclude: [
                        'me/sungbin/day2/**',
                        'me/sungbin/day4/**',
                        'me/sungbin/day7/**'
                ])
        )
    }
}

